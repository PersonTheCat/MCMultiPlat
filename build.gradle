import personthecat.buildtools.OverwriteValidator

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'local.personthecat', name: 'cavegenerator-plugins'
        classpath group: 'gradle.plugin.com.github.jengelman.gradle.plugins', name: 'shadow', version: '7.0.0'
        classpath group: 'org.eclipse.equinox', name: 'common', version: '3.2.0-v20060603'
        classpath group: 'org.eclipse.core', name: 'org.eclipse.core.resources', version: '3.7.100'
    }
}

allprojects {
    version = mod_version

    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'
    apply plugin: OverwriteValidator

    repositories {
        // So we can conveniently read dependencies from GH.
        maven { url = 'https://jitpack.io' }
        mavenCentral()
    }

    configurations {
        bundle
        implementation.extendsFrom bundle
    }

    dependencies {
        bundle group: 'com.github.personthecat', name: 'hjson-java', version: '4ff07499a4'
        bundle group: 'com.github.personthecat', name: 'fresult', version: '441c74d6b8'
        implementation 'org.jetbrains:annotations:16.0.2'

        compileOnly group: 'local.personthecat', name: 'cavegenerator-plugins'
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }

    jar {
        from {
            configurations.bundle.collect { it.isDirectory() ? it : zipTree(it) }
        }
        archivesBaseName = "$archive_base-$platform"
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    shadowJar {
        configurations = [project.configurations.bundle]
        relocate 'org.hjson', 'caveshadow.org.hjson'
        relocate 'personthecat.fresult', 'caveshadow.personthecat.fresult'
        classifier = ''
    }

    build.dependsOn shadowJar

    tasks.withType(Test) {
        useJUnitPlatform()
        ignoreFailures = false
        failFast = true
    }
}
